'##################################
'##  Fruit Catch Game            ##
'##         2020/05/04  DiNOV    ##
'## Ref:ALL ABOUT Pi STARTER     ##
'##      by DenpaShinbun-sha     ##
'##        ISBN978-4-86406-034-9 ## 
'##################################
ACLS:GCLS &HFF005500
'BGMPLAY 0,12,50
PLAYER=2544:OBAKE=3068:APPLE=2051:BANANA=2053:COIN=2278
X=320:Y=300
SCORE=0:LEVEL=1:PRE_LEVEL=1
SPSET PLAYER OUT N
SPCOL N
WHILE 1
 LOCATE 0,2
 PRINT "SCORE:";SCORE;" LEVEL:";LEVEL;
 IF MAINCNT MOD 40==0 THEN
  SPSET APPLE OUT M
  SPCOL M
  SPANIM M,"XY",1,RND(640),-1,-5*60,RND(640),376,1
  FOR i=1 TO LEVEL
   SPSET OBAKE OUT M
   SPCOL M,-1,-8,2,2
   SPANIM M,"XY",1,RND(640),-1,-4*60,RND(640),376,1
  NEXT
 ENDIF
 IF MAINCNT MOD 240==0 THEN 'バナナは貴重で出現回数が少ない
  SPSET BANANA OUT M
  SPCOL M
  SPANIM M,"XY",1,RND(320),-1,-5*60,RND(320),376,1
 ENDIF
 IF MAINCNT MOD 480==0 THEN 'COINは貴重で出現回数がとっても少ない
  SPSET COIN OUT M
  SPCOL M
  SPANIM M,"XY",1,RND(320),-1,-5*20,RND(320),376,1
 ENDIF
 FOR I=1 TO 511
  IF SPUSED(I)==FALSE THEN CONTINUE
  SPOFS I OUT TX,TY
  IF TY>=376 THEN SPCLR I
 NEXT
 STICK 0 OUT SX,SY 'PLAYERの動作
 IF SX<-0.5 THEN X=X-4
 IF SX>0.5  THEN X=X+4
 IF X<8 THEN X=8
 IF X>631 THEN X=631
 IF SY<-0.5 THEN Y=Y-4
 IF SY>0.5  THEN Y=Y+4
 IF Y<8 THEN Y=8
 IF Y>370 THEN Y=370
 SPOFS N,X,Y
 HIT=SPHITSP(N)
 IF HIT!=-1 THEN
  SPCHR HIT OUT DN
  IF DN==APPLE THEN
   SPCLR HIT
   SCORE=SCORE+1 'リンゴは１点
 '  BEEP 71
   LEVEL=(SCORE DIV 3)+1
   IF LEVEL>PRE_LEVEL THEN 
    PRE_LEVEL=LEVEL
'	BEEP 38
   ENDIF
  ENDIF
  IF DN==BANANA THEN
   SPCLR HIT
   SCORE=SCORE+2 'バナナは2点
 '  BEEP 71
   LEVEL=(SCORE DIV 3)+1
   IF LEVEL>PRE_LEVEL THEN 
    PRE_LEVEL=LEVEL
'	BEEP 38
   ENDIF
  ENDIF
  IF DN==COIN THEN
   SPCLR HIT
   SCORE=SCORE+3 'COINは３点
 '  BEEP 7
   LEVEL=(SCORE DIV 3)+1
   IF LEVEL>PRE_LEVEL THEN 
    PRE_LEVEL=LEVEL
'	BEEP 38
   ENDIF
  ENDIF
  IF DN==OBAKE THEN 'おばけに当たるとゲーム終了
  ' BEEP 37
   BREAK
  ENDIF
 ENDIF
 VSYNC
WEND
LOCATE 0,42
PRINT "GAME OVER  ==  PRESS [F5] TO RUN AGAIN"
GCLS &HFF775500
BGMSTOP
PRINT
