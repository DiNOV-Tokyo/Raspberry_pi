'#######################################
'##    Rolling Ball Game              ##
'##         2020.05.05   DiNOV        ##
'##  Ref:Nikkei Software 2018.11      ##
'#######################################
ACLS
GCLS #BLUE
GFILL 16,16,640-16,360-16,#GREEN
BGMPLAY 0,13,50
VOLUME 120
SPSET 2412 OUT GOAL
SPSET 2392 OUT BALL
GX=640*5/6
GY=360/6
X=640/6
Y=360*5/6
'SPX=200
'SPY=200
'SPC=0
'SPD=0
SPOFS GOAL,GX,GY
SPOFS BALL,X,Y
SPANIM BALL,"I",8,2392,8,2393,0
SPCOL GOAL
SPCOL BALL
FOR I=0 TO 60
 HX=RND(36)*16+16
 HY=RND(20)*16+16
 IF ABS(HX-GX)<64 AND ABS(HY-GY)<64 THEN CONTINUE
 IF ABS(HX-X)<64 AND ABS(HY-Y)<64 THEN CONTINUE
 GFILL HX,HY,HX+15,HY+15,#WHITE
 GFILL HX+1,HY+1,HX+13,HY+13,#BLACK
NEXT
FOR I=0 TO 2
 WAIT 60
 LOCATE 38,20
 PRINT MID$("READYGO!!!     ",I*5,5)
 BEEP 18
NEXT 
TIMECNT=0
X1=0
Y1=0
GOVER=0
WHILE 1
 A$="TIME:"+FORMAT$("%6.3F",TIMECNT)+"SEC
 LOCATE 40-LEN(A$)/2,1
 PRINT A$
 STICK OUT XA,YA
 XA=XA*3
 YA=YA*3
 'XA=XA*10
 'YA=YA*10
 X1=X1*0.99
 Y1=Y1*0.99
 X1=X1+(XA/40)
 Y1=Y1+(YA/40)
 X=X+X1
 Y=Y+Y1
 SPOFS BALL,X,Y
 C=GSPOIT(X,Y)
 RGBREAD C OUT R,G,B
 IF G==0 THEN GOVER=1:BREAK
 GPSET X,Y,#LIME
 HIT=SPHITSP(BALL)
 IF HIT==GOAL THEN BREAK
 TIMECNT=TIMECNT+(1/60)
 VSYNC 1
WEND
IF GOVER THEN
 A$="GAME OVER"
 BEEP 6
ELSE
 A$="GOAL!"
 BEEP 90
ENDIF
LOCATE 40-LEN(A$)/2,20
PRINT A$
BGMSTOP
END
 

